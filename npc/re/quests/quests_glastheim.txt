//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= NPCs related to Glastheim (Nightmare)
//===== By: ==================================================
//= Ridley8819
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Additional Comments: =================================
//= 1.0 Initial release [Ridley8819]
//= 1.1 Dialogue cleanup [Aleos]
//============================================================

glast_01,195,273,4	script	华金的追随者#n_gh	123,{
	mes "[华金的追随者]";
	mes "我有一个很好奇的问题.";
	next;
	mes "[华金的追随者]";
	mes "是什么导致古城充满了如此强大的黑暗力量呢?";
	next;
	if( select( "我并不关心:这很有趣, 请告诉我更多" ) == 2 ){
		mes "[华金的追随者]";
		mes "感谢华金, 让我发现了这个古城, 它被一个强大的黑暗力量笼罩着.";
		next;
		mes "[华金的追随者]";
		mes "你说什么? 你想去那里探险吗?";
		next;
		if( select( "我没兴趣探险:我已经准备好去探险!" ) == 2 ){
			mes "[华金的追随者]";
			mes "当然, 这并不是一项免费服务! 我需要有一些报酬. 呵呵...";
			next;
			mes "[华金的追随者]";
			mes "我可以把你送到2个地方. 你想让我送你去哪个地方呢?";
			next;
			switch( select( "取消:修道院墓地 (Lv100~130) - 5000z:走廊第二层 (Lv140+) - 10000z" ) ){
				case 1:
					mes "[华金的追随者]";
					mes "好吧, 当你改变主意的时候再回来. 我想你会有很多话要和我说...";
					close;
				case 2:
					.@map$ = "gl_chyard_";
					.@x = 148;
					.@y = 144;
					.@zeny = 5000;
					break;
				case 3:
					.@map$ = "gl_cas02_";
					.@x = 104;
					.@y = 31;
					.@zeny = 10000;
					break;
			}
			if( Zeny < .@zeny ){
				mes "[华金的追随者]";
				mes "什么? 你以为这是免费服务吗?";
				close;
			}
			Zeny -= .@zeny;
			warp .@map$, .@x, .@y;
			end;
		}
	}
	mes "[华金的追随者]";
	mes "好吧, 如果你对它不感兴趣, 没关系.";
	close;
}

gl_chyard_,148,149,4	script	华金的追随者#n_gh1	123,{
	mes "[华金的追随者]";
	mes "你想回去吗?";
	mes "好吧, 让我们离开这里.";
	next;
	if( select( "我不想和你说话:请带我离开这里" ) == 1 ){
		mes "[华金的追随者]";
		mes "好吧, 如果你想要离开再来告诉我.";
		close;
	}
	warp "glast_01", 210, 269;
	end;
}

gl_cas02_,104,36,4	duplicate(华金的追随者#n_gh1)	华金的追随者#n_gh2	123
