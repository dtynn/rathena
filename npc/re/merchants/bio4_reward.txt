//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script =======================================
//= Lighthalzen rewards NPCs
//===== Description: =========================================
//= Enchant and reward NPCs from Bio instance/dungeon item drop.
//===== Changelogs: ==========================================
//= 1.0 Moved the NPCs from WolfchevLaboratory.txt. [Capuche]
//============================================================

// Weird Old Man (Rewards)
//============================================================
lighthalzen,342,291,4	script	古怪的老人#Bio4Reward	4_M_EINOLD,{
	if ((MaxWeight - Weight) < 2800 || !checkweight(1201, 1)) {
		mes "^FF0000 检查你的库存, 你有太多物品而导致不能继续. ^000000";
		close;
	}
	mes "[古怪的老人]";
	mes "啊, 现在的年轻人, 是如此的粗鲁和没礼貌.";
	next;
	if (select("不理睬他", "和他对话") == 1) {
		mes "[古怪的老人]";
		mes "你, 是个很坏的年轻人!";
		close;
	}
	mes "["+ strcharinfo(0) +"]";
	mes "先生, 你还好吗??";
	next;
	mes "[古怪的老人]";
	mes "....?? 你不像其他年轻人. 你很有礼貌! 你知道我发生了什么..?";
	next;
	switch(rand(1,2)) {
	case 1:
		mes "[古怪的老人]";
		mes "我在探望儿子的路上, 那个地方很远, 我需要坐飞机去, 和..";
		next;
		mes "[古怪的老人]";
		mes "那些年轻人...实在太吵...我站着却没一个人给我让座, 他们还大声说话, 让我很烦.";
		next;
		mes "[古怪的老人]";
		mes "如果我年轻10岁的话, 我真想给他们上一课.";
		next;
		mes "[古怪的老人]";
		mes "但...我又不喜欢时间流逝...";
		next;
		mes "[古怪的老人]";
		mes "所以, 你...你会怎么做??";
		next;
		mes "[古怪的老人]";
		mes "在公共的地方, 你应该要保持安静, 看见老人和体弱者应该让座.";
		next;
		break;
	case 2:
		mes "[古怪的老人]";
		mes "我在探望儿子的路上, 那个地方很远, 我需要坐飞机去, 和..";
		next;
		mes "[古怪的老人]";
		mes "那些年轻人...实在太吵...我站着却没一个人给我让座, 他们还大声说话, 让我很烦.";
		next;
		mes "[古怪的老人]";
		mes "如果我年轻10岁的话, 我真想给他们上一课.";
		next;
		mes "[古怪的老人]";
		mes "但...我又不喜欢时间流逝...";
		next;
		mes "[古怪的老人]";
		mes "所以, 你...你会怎么做??";
		next;
		mes "[古怪的老人]";
		mes "在公共的地方, 你应该要保持安静, 看见老人和体弱者应该让座, 对吗?";
		next;
		mes "[古怪的老人]";
		mes "我说的对不对?!";
		next;
		switch(rand(1,2)) {
		case 1:
			mes "[古怪的老人]";
			mes "哦, 然后! 你知道吗...?";
			next;
			mes "[古怪的老人]";
			mes "有一次我走在路上, 一些男孩随地吐痰, 正好有位女孩路过...啧啧!";
			next;
			mes "[古怪的老人]";
			mes "这些人的行为想只狗... 不知道怎么想的?? 这样的行为是很不道德的...啧啧.";
			next;
			switch(rand(1,2)) {
			case 1:
				mes "[古怪的老人]";
				mes "哦, 然后! 你知道吗...?";
				next;
				break;
			case 2:
				mes "[古怪的老人]";
				mes "哦, 然后! 你知道吗...?";
				next;
				mes "[古怪的老人]";
				mes "现在的年轻人不那么有耐心!!!!";
				next;
				mes "[古怪的老人]";
				mes "通常, 他们说他们会改, 可一直不会更正错误, 就连找工作也成为难事...啧啧.";
				next;
				mes "[古怪的老人]";
				mes "找一份工作并不容易, 被录取是很困难的. 但他们一个星期都坚持不了, 最后放弃了工作.";
				next;
				mes "[古怪的老人]";
				mes "这些人是多么不耐烦和不负责任!!";
				next;
				mes "[古怪的老人]";
				mes "哦, 然后! 你知道吗...?";
				next;
				break;
			}
		case 2:
			mes "[古怪的老人]";
			mes "哦, 然后! 你知道吗...?";
			next;
			break;
		}
	}
	mes "^E7A43D老人一次又一次不断地说话.^000000";
	next;
	mes "[古怪的老人]";
	mes "嗯...你不像普通的年轻人, 看起来很有耐心, 我喜欢你这样的人!!!";
	next;
	mes "[古怪的老人]";
	mes "现在我虽然已经老了, 但在我年轻的时候, 我是一位著名的商人.";
	next;
	mes "[古怪的老人]";
	mes "在这一段时间里, 我停止了生意,";
	mes "但最近我之前的客人联系我并且问我";
	next;
	mes "[古怪的老人]";
	mes "如果我能给他一些物品 '勇士的意志', '血的饥渴', '亡者的寒气'.";
	next;
	mes "[古怪的老人]";
	mes "我喜欢你很有礼貌, 和大多数年轻人不一样, 所以如果你能把我的话传达, 我会给你这些物品.";
	next;
	while(1) {
		switch(select("我去收集, 我会回来的", "显示我的装备", "显示我的武器")) {
		case 1:
			mes "[古怪的老人]";
			mes "OK, 年轻人, 回头见.";
			close;
		case 2:
			mes "[古怪的老人]";
			mes "所以, 你对装备有兴趣哦.";
			next;
			mes "[古怪的老人]";
			mes "这些都是我的装备.";
			next;
			for(.@i = 1; .@i < 8; .@i++) {
				if (((.@i == 1 || .@i == 5) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 35)) || 
				    ((.@i == 2 || .@i == 3) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 22)) ||
				    ((.@i == 4 || .@i == 7) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400)) ||
				    ((.@i == 6) && (countitem(6469) >= 100) && (countitem(6470) >= 50) && (countitem(6471) >= 30))) {
					.@armor$[.@i] = "^0000FF";
				} else {
					.@armor$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"给我看其他物品.",
					""+.@armor$[1]+"守护巨人钢盾^000000",
					""+.@armor$[2]+"葛帔尼亚的古书(水)^000000",
					""+.@armor$[3]+"誓约之书(第2卷)^000000",
					""+.@armor$[4]+"救援斗篷^000000",
					""+.@armor$[5]+"刺客的手铐^000000",
					""+.@armor$[6]+"绿色研究服^000000",
					""+.@armor$[7]+"古代的金饰^000000"
				)) {
				case 1:
					mes "[古怪的老人]";
					mes "请看.";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<守护巨人钢盾[1]>>";
					mes "遭受大型怪物的伤害 -5%, 当精练能力+9时减少量 +5%.";
					mes "盾牌 / Def 130 / 重量 280 / 要求等级 100 / 洞数 1 / 三转剑士系列";
					callsub L_Reward, 2160, 100, 50, 35; //Giant_Shield
					break;
				case 3:
					mes "<<葛帔尼亚的古书(水)[1]>>";
					mes "INT + 1, MDEF + 2. 基础INT 120时 MATK +10, MHP+800.";
					mes "与暴风雪法杖一起装备时, 冰雾/霜冻爆破/暴风雪的变动咏唱减少%为盾牌精炼度的4倍.";
					mes "盾牌 / Def 30 / 重量 100 / 要求等级 100 / 洞数 1 / 大法师";
					callsub L_Reward, 2161, 100, 50, 22; //Geffenia_Book_Water
					break;
				case 4:
					mes "<<誓约之书第2卷[1]>>";
					mes "MDEF + 5, 可使用奥丁之神力 2Lv. 自己使用的治愈量 +5%.";
					mes "盾牌 / Def 20 / 重量 50 / 要求等级 140 / 洞数 1 / 大主教";
					callsub L_Reward, 2162, 100, 50, 22; //Bible_Of_Promise2
					break;
				case 5:
					mes "<<救援斗篷[0]>>";
					mes "可使用金蝉脱壳 1Lv, 该技能的冷却时间增加至 30秒, 装备时所有技能的消耗SP为2倍, 卸除时 SP - 1200.";
					mes "披肩 / Def 1 / 重量 40 / 要求等级 Lv 130 / 洞数 0 / 大法师/大主教";
					callsub L_Reward, 2582, 100, 50, 35; //Salvage_Cape
					break;
				case 6:
					mes "<<刺客的手铐[1]>>";
					mes "MSP + 20, CRI + 3. 与奎师那之刃一起装备时, ATK +50, 音速投掷的攻击力 +50%, FLEE - 30. 与脉轮之刃一起装备时, CRI +4, 必杀攻击力 +40%, MHP - 10%.";
					mes "装饰品 / Def 3 / 重量 40 / 要求等级 Lv 100 / 洞数 1 / 十字切割者";
					callsub L_Reward, 2892, 300, 300, 400; //Assassin_Handcuffs
					break;
				case 7:
					mes "<<绿色研究服[1]>>";
					mes "DEX + 1, MSP + 30. 与手术刀一起装备后消灭人类系/动物系怪物时, 有机率掉落不死心脏/酒精. 绿色研究服的精炼度会略微提升掉宝率.";
					mes "铠甲 / Def 66 / 重量 66 / 要求等级 Lv 100 / 洞数 1 / 基因学者";
					callsub L_Reward, 15044, 100, 50, 30; //Green_Operation_Coat
					break;
				case 8:
					mes "<<古代的金饰[1]>>";
					mes "DEX + 1, MSP + 30. 剑士,商人,盗贼系列时 ATK +8%, 魔法师,服侍系列时 MATK +8%, 使用的治愈量 +7%, 弓箭手系列时 DEX +3, 远距离物理攻击力 +10%.";
					mes "头盔 / Def 66 / 重量 66 / 要求等级 Lv 100 / 洞数 1 / 三转弓箭手系列";
					callsub L_Reward, 18570, 300, 300, 400; //Ancient_Gold_Deco
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
			break;
		case 3:
			mes "[古怪的老人]";
			mes "所以, 你有兴趣的武器.";
			next;
			mes "[古怪的老人]";
			mes "这些是我有的武器";
			next;
			for(.@i = 1; .@i < 15; .@i++) {
				if (((.@i == 1) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 10)) ||
				((.@i == 2) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 85)) ||
				((.@i == 3 || .@i == 4 || (.@i >= 8 && .@i < 11) || .@i >= 12) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 35)) ||
				((.@i == 5 || .@i == 7 || .@i == 11) && (countitem(6469) >= 50) && (countitem(6470) >= 100) && (countitem(6471) >= 40)) ||
				((.@i == 6) && (countitem(6469) >= 300) && (countitem(6470) >= 300) && (countitem(6471) >= 400))) {
					.@weapon$[.@i] = "^0000FF";
				} else {
					.@weapon$[.@i] = "^FF0000";
				}
			}
			while(1) {
				switch(select(
					"看看其他物品.",
					""+.@weapon$[1]+"特务拳刃^000000",
					""+.@weapon$[2]+"斩首拳刃^000000",
					""+.@weapon$[3]+"火焰战斧^000000",
					""+.@weapon$[4]+"急冻战斧^000000",
					""+.@weapon$[5]+"钙矿石长矛^000000",
					""+.@weapon$[6]+"守护巨人长矛^000000",
					""+.@weapon$[7]+"寒气篇魔法书^000000",
					""+.@weapon$[8]+"元气之光魔杖^000000",
					""+.@weapon$[9]+"绿光魔短剑^000000",
					""+.@weapon$[10]+"红光魔短剑^000000",
					""+.@weapon$[11]+"嗜血十字锤^000000",
					""+.@weapon$[12]+"元戎弩^000000",
					""+.@weapon$[13]+"超异能十字弓^000000",
					""+.@weapon$[14]+"束缚之弓^000000"
				)) {
				case 1:
					mes "[古怪的老人]";
					mes "看看.";
					next;
					.@break = 1;
					break;
				case 2:
					mes "<<特务拳刃[1]>>";
					mes "基础LUK每加2点, HIT +1.";
					mes "拳刃 / ATK 170 / 重量 120 / 武器等级 Lv 3 / 要求等级 Lv 100 / 洞数 1 / 十字切割者";
					callsub L_Reward, 1290, 50, 100, 10; //Agent_Katar
					break;
				case 3:
					mes "<<斩首拳刃[1]>>";
					mes "DEX + 2. FLEE - 30. 对人类系的敌人伤害 +50%. 十字斩的伤害 +30%.";
					mes "拳刃 / ATK 200 / 重量 150 / 武器等级 Lv 4 / 要求等级 Lv 140 / 洞数 1 / 十字切割者";
					callsub L_Reward, 1291, 50, 100, 85; //Guillotine_Katar
					break;
				case 4:
					mes "<<火焰战斧[1]>>";
					mes "近距离物理攻击时有机率对敌人和自己发动着火.";
					mes "双手斧 / ATK 250 / 重量 190 / 火属性 / 武器等级 Lv 4 / 要求等级 Lv 95 / 洞数 1 / 转生剑士/商人系列";
					callsub L_Reward, 1392, 50, 100, 35; //Ygnus_Stale
					break;
				case 5:
					mes "<<急冻战斧[1]>>";
					mes "近距离物理攻击时有机率对敌人和自己发动结晶";
					mes "双手斧 / ATK 250 / 重量 190 / 水属性 / 武器等级 Lv 4 / 要求等级 Lv 95 / 洞数 1 / 转生剑士/商人系列";
					callsub L_Reward, 1393, 50, 100, 35; //End_Sektura
					break;
				case 6:
					mes "<<钙矿石长矛[1]>>";
					mes "MSP - 100, [加农炮]攻击力 +10%, 该技能攻击力额外增加到精炼值的3倍";
					mes "单手枪 / ATK 180 / 重量 160 / 武器等级 Lv 4 / 要求等级 Lv 100 / 洞数 1 / 皇家卫士";
					callsub L_Reward, 1435, 50, 100, 40; //Cannon_Spear
					break;
				case 7:
					mes "<<守护巨人长矛[0]>>";
					mes "ASPD - 10, 卸除时 SP - 600. 螺旋击刺的再使用延迟 +20秒. 基础STR 120以上时, ATK +300.";
					mes "双手枪 / ATK 20 / 重量 2000 / 武器等级 Lv 4 / 要求等级 Lv 140 / 洞数 0 / 符文骑士";
					callsub L_Reward, 1490, 300, 300, 400; //Giant_Lance
					break;
				case 8:
					mes "<<寒气篇魔法书[2]>>";
					mes "MATK + 160, DEX + 1. 冰箭术, 钻石星尘的攻击力增加%幅度为精炼度的3倍, 该技能的消耗SP增加幅度为精炼度的5倍.";
					mes "书 / ATK 100 / 重量 100 / 水属性 / 武器等级 Lv 4 / 要求等级 Lv 100 / 洞数 2 / 元素使";
					callsub L_Reward, 1584, 50, 100, 40; //Chilly_Spell_Book
					break;
				case 9:
					mes "<<元气之光魔杖[1]>>";
					mes "MATK + 160. 依精炼度治愈量和治愈时所消耗的SP也随之大幅度的增加. 不会损坏.";
					mes "单手杖 / ATK 30 / 重量 40 / 圣属性 / 武器等级 Lv 4 / 要求等级 Lv 110 / 洞数 1 / 大主教.";
					callsub L_Reward, 1659, 50, 100, 35; //Recovery_Light
					break;
				case 10:
					mes "<<绿光魔短剑[0]>>";
					mes "MATK + 80. 近距离物理攻击时有机率对目标发动冰冻, 依精炼度冰冻机率会增加.";
					mes "短剑 / ATK 160 / 重量 50 / 风属性 / 武器等级 Lv 4 / 要求等级 Lv 110 / 洞数 0 / 转生剑士/商人/盗贼系列";
					callsub L_Reward, 13069, 50, 100, 35; //Aztoe_Nail
					break;
				case 11:
					mes "<<Scarletto Nail[0]>>";
					mes "MATK + 80. For Physical Melee Attack, cast Stone curse to the target with a certain chance. The chance is affected by Refine Lv.";
					mes "Dagger / ATK 160 / Weight 50 / Fire Property / Weapon Lv 4 / Required Lv 110 / Socket 0 / for High Swordman/Merchant/Theif Classes";
					callsub L_Reward, 13070, 50, 100, 35; //Scarletto_Nail
					break;
				case 12:
					mes "<<嗜血十字锤[0]>>";
					mes "近距离物理攻击时有机率发动自动念咒 毁灭烈焰1Lv, 依精炼度该技能的发动机率也会增加";
					mes "钝器 / ATK 170 / 重量 150 / 暗属性 / 武器等级 Lv 4 / 要求等级 Lv 100 / 洞数 0 / 转生服侍系列";
					callsub L_Reward, 16017, 50, 100, 40; //Bloody_Cross
					break;
				case 13:
					mes "<<元戎弩[2]>>";
					mes "三角射击的攻击力增加%幅度为精炼度的2倍, 该技能的消耗SP也会增加.";
					mes "弓 / ATK 150 / 重量 110 / 武器等级 Lv 4 / 要求等级 Lv 100 / 洞数 2 / 逐影";
					callsub L_Reward, 18109, 50, 100, 35; //Catapult
					break;
				case 14:
					mes "<<超异能十字弓[2]>>";
					mes "箭雨风暴攻击力增加%幅度为精炼度的5倍, 该技能的消耗SP也会增加, 基础AGI 120时 ASPD+1.";
					mes "弓 / ATK 160 / 重量 90 / 武器等级 Lv 4 / 要求等级 Lv 110 / 洞数 2 / 游侠";
					callsub L_Reward, 18110, 50, 100, 35; //Big_CrossBow
					break;
				case 15:
					mes "<<束缚之弓[2]>>";
					mes "DEX +1, 物理攻击时有机率发动自动念咒 易燃之网 1Lv.";
					mes "弓 / ATK 150 / 重量 150 / 武器等级 Lv 3 / 要求等级 Lv 120 / 洞数 2 / 三转弓箭手系列";
					callsub L_Reward, 18111, 50, 100, 35; //Creeper_Bow
					break;
				}
				if (.@break) {
					.@break = 0;
					break;
				}
			}
		}
	}
	close;

L_Reward:
	setarray .@amount[0], getarg(1), getarg(2), getarg(3);
	next;
	mes "我需要"+ .@amount[0] +" 勇士的意志, "+ .@amount[1] +" 血的饥渴, "+ .@amount[2] +" 亡者的寒气";
	next;
	if (countitem(6469) >= .@amount[0] && countitem(6470) >= .@amount[1] && countitem(6471) >= .@amount[2]) {
		if (select("不换", "换取") == 1) {
			mes "[古怪的老人]";
			mes "好的..";
			close;
		}
		emotion ET_PROFUSELY_SWEAT, "Weird old man#Bio4Reward";
		progressbar "FF00FF", 2;
		delitem 6469, .@amount[0]; //Will_Of_Warrior
		delitem 6470, .@amount[1]; //Blood_Thirst
		delitem 6471, .@amount[2]; //Goast_Chill
		getitem getarg(0), 1;
			mes "[古怪的老人]";
			mes "你去那里.访问我的时候, 我们可以谈谈";
		close;
	}
	return;
}

// Sorcerer (Rewards)
//============================================================
lhz_cube,233,24,4	script	巫师#Bio4Reward	4_M_UMDANCEKID,{
	if ((MaxWeight - Weight) < 1200 || !checkweight(1201, 1)) {
		mes "^FF0000 无法完成任务因为你身上太重或者东西太多. ^000000";
		close;
	}
	disable_items;
	mes "[布丁]";
	mes "... .... ...";
	next;
	switch(select("Umba umba humbaa", "你在这里做什么?", "这是迄今为止发现的设备?", "给你, 先生")) {
	case 1:
		emotion ET_FRET;
		mes "[布丁]";
		mes "嘿, 对不起, 但我不是汶巴拉人!你真粗鲁";
		close;
	case 2:
		mes "[布丁]";
		mes "正如你所看到的, 我是个巫师, 我的名字叫布丁.";
		next;
		select("....?");
		emotion ET_FRET;
		mes "[布丁]";
		mes "... 那是什么脸?我开始感到不愉快..";
		next;
		emotion ET_OK;
		mes "[布丁]";
		mes "前两天,我感到奇怪, 古怪色彩.所以我也跟着说, 它把我带到这里";
		next;
		select("我看到.");
		mes "[布丁]";
		mes "好吧, 让我继续.请别打扰我.";
		next;
		mes "[布丁]";
		mes "我进了这里的实验室做研究, 哦天哪!我发现那些怪物, 其中像从“吉芬地下城”的魔物充斥在其中.";
		next;
		mes "[布丁]";
		mes "从这些怪物中, 我能找到稀有物品 '^F2766E勇士的意志^000000' '^952420血的饥渴^000000' '^88C6F7亡者的寒气^000000' 他们不会出现在其他地方.";
		next;
		emotion ET_QUESTION, "Sorcerer#Bio4Reward";
		mes "[布丁]";
		mes "很多时间对我自己的研究后, 我终于想出了一个办法来使用这些物品.你想知道吗?";
		next;
		switch(select("呵呵....","当然了, 我真的很想知道")) {
		case 1:
			emotion ET_HUK, 1;
			emotion ET_FRET, "Sorcerer#Bio4Reward";
			mes "[布丁]";
			mes "噢, 我的!你睡着了, 你叫我什么呢?好吧, 反正我觉得懒得再解释, 所以我只会继续下去";
			next;
			break;
		case 2:
			emotion ET_SMILE, "Sorcerer#Bio4Reward";
			mes "[布丁]";
			mes "不错, 你仔细听我说话然后我会一一解释.";
			next;
			break;
		}
		mes "[布丁]";
		mes "Those '^F2766E勇士的意志^000000' and '^952420血的饥渴^000000'..... 可以用来做保暖的材料.";
		next;
		mes "[布丁]";
		mes "而 '^F2766E勇士的意志^000000' 能够在装备的第四槽为你的装备增加能力,";
		next;
		mes "[布丁]";
		mes "还有 '^952420血的饥渴^000000' 可以在装备第三槽进行附魔.";
		next;
		mes "[布丁]";
		mes "而且!!!";
		mes "'^88C6F7亡者的寒气^000000' 是一种冷冻材料.";
		mes "它允许从'^F2766E勇士的意志^000000' 或者 '^952420血的饥渴^000000'返回能量.";
		next;
		mes "[布丁]";
		mes "你说什么?没有听起来有趣吗?但是, 有一个问题...";
		next;
		mes "[布丁]";
		mes "只能从实验室的那些怪物身上获取这些材料";
		next;
		select("那么, 结论是什么?");
		emotion ET_QUESTION, 1;
		emotion ET_FRET;
		mes "[布丁]";
		mes "啧啧......我告诉过你在我说话的时候不要打断我, 好吧, 我会回答这个重要的问题的.";
		next;
		mes "[布丁]";
		mes "如果你给我带来10个 '^F2766E勇士的意志^000000' 或者 10个 '^952420血的饥渴^000000', 我会帮你进行附魔, 而这些材料可以在实验室里获得.";
		next;
		mes "[布丁]";
		mes "如果你不满意你拥有强大的力量, 给我带来10个 '^88C6F7亡者的寒气^000000', 我将给你的装备进行附魔";
		next;
		mes "[布丁]";
		mes "你说什么?有两个选项.....相当不错, 是吧?";
		next;
		mes "[布丁]";
		mes "如果您获得 '^F2766E勇士的意志^000000' 或者 '^952420血的饥渴^000000', 并且还有可以附魔的装备, 我将对你的装备进行附魔, 快点拿来给我, 我会在这里等你.";
		close;
	case 3:
		mes "[布丁]";
		mes "这是迄今发现的....";
		next;
		mes "- 检查 -";
		mes "布丁从口袋里拿出了一张纸";
		next;
		mes "[布丁]";
		mes "让我看看....";
		mes "6盔甲, 14武器";
		next;
		while(1) {
			switch(select("好吧, 我需要知道更多信息", "6个盔甲", "14个武器")) {
			case 1:
				mes "[布丁]";
				mes "如果您获得 '^F2766E勇士的意志^000000' 或者 '^952420血的饥渴^000000', 并且还有可以附魔的装备, 我将对你的装备进行附魔, 快点拿来给我, 我会在这里等你.";
				close;
			case 2:
				mes "< 防具列表 >";
				mes "守护巨人钢盾[1]";
				mes "葛帔尼亚的古书(水)[1]";
				mes "誓约之书第2卷[1]";
				mes "救援斗篷[0]";
				mes "刺客的手铐[1]";
				mes "绿色研究服[1]";
				mes "古代的金饰[1]";
				next;
				mes "[布丁]";
				mes "到目前为止, 这些都是发现了的装甲.难道你想看到其他的东西?";
				next;
				break;
			case 3:
				mes "< 武器列表 >";
				mes "特务拳刃[1]";
				mes "斩首拳刃[1]";
				mes "火焰战斧[1]";
				mes "急冻战斧[1]";
				mes "钙矿石长矛[1]";
				mes "守护巨人长矛[0]";
				mes "寒气篇魔法书[2]";
				mes "元气之光魔杖[1]";
				mes "绿光魔短剑[0]";
				mes "红光魔短剑[0]";
				mes "嗜血十字锤[0]";
				mes "元戎弩[2]";
				mes "超异能十字弓[2]";
				mes "束缚之弓[2]";
				next;
				mes "[布丁]";
				mes "到目前为止, 这些都是发现了的武器.难道你想看到其他的东西?";
				next;
				break;
			}
		}
		close;
	case 4:
		emotion ET_SPARK;
		mes "[布丁]";
		mes "啊啊啊啊, 你没有把它们带来吗?";
		next;
		break;
	}
	mes "[布丁]";
	mes "好啊, 你想要什么能力?";
	next;
	setarray .@parts[0], EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_TOP;
	for(.@i = 0; .@i < 7; .@i++) {
		if (!getequipisequiped(.@parts[.@i])) {
			switch(.@i) {
				case 0:
					.@equipped$[.@i] = "^999999衣服-[没有装备]^000000";
					break;
				case 1:
					.@equipped$[.@i] = "^999999左手-[没有装备]^000000";
					break;
				case 2:
					.@equipped$[.@i] = "^999999右手-[没有装备]^000000";
					break;
				case 3:
					.@equipped$[.@i] = "^999999披肩-[没有装备]^000000";
					break;
				case 4:
					.@equipped$[.@i] = "^999999装饰品-[没有装备]^000000";
					break;
				case 5:
					.@equipped$[.@i] = "^999999装饰品-[没有装备]^000000";
					break;
				case 6:
					.@equipped$[.@i] = "^999999头下-[没有装备]^000000";
					break;
			}
		} else {
			.@equipped$[.@i] = getequipname(.@parts[.@i]);
		}
	}
	switch(select("我改变主意了.", .@equipped$[0], .@equipped$[1], .@equipped$[2], .@equipped$[3], .@equipped$[4],
	.@equipped$[5], .@equipped$[6])) {
	case 1:
		mes "[布丁]";
		mes "好吧, 我期待着这样做, 但我不能帮助";
		close;
	case 2:
		if (!getequipisequiped(EQI_ARMOR)) {
			mes "[布丁]";
			mes "嘿, 你的衣服呢?";
			close;
		}
		.@part = EQI_ARMOR;
		break;
	case 3:
		if (!getequipisequiped(EQI_HAND_L)) {
			mes "[布丁]";
			mes "你的左手没有任何装备";
			close;
		}
		.@part = EQI_HAND_L;
		break;
	case 4:
		if (!getequipisequiped(EQI_HAND_R)) {
			mes "[布丁]";
			mes "你的右手没有任何装备.";
			close;
		}
		.@part = EQI_HAND_R;
		break;
	case 5:
		if (!getequipisequiped(EQI_GARMENT)) {
			mes "[布丁]";
			mes "呃, 你的披肩呢?";
			close;
		}
		.@part = EQI_GARMENT;
		break;
	case 6:
		if (!getequipisequiped(EQI_ACC_L)) {
			mes "[布丁]";
			mes ".....你的装饰品呢?";
			close;
		}
		.@part = EQI_ACC_L;
		break;
	case 7:
		if (!getequipisequiped(EQI_ACC_R)) {
			mes "[布丁]";
			mes ".....你的装饰品呢?";
			close;
		}
		.@part = EQI_ACC_R;
		break;
	case 8:
		if (!getequipisequiped(EQI_HEAD_TOP)) {
			mes "[布丁]";
			mes "嘿, 这部位好像没有装备.";
			close;
		}
		.@part = EQI_HEAD_TOP;
		break;
	}
	.@refine_count = getequiprefinerycnt(.@part);
	.@equip_item = getequipid(.@part);
	.@lhz_max_num = 4000;
	if (.@equip_item == 13069 || .@equip_item == 1291 || .@equip_item == 1392 || 
	    .@equip_item == 1393 || .@equip_item == 1435 || .@equip_item == 1490 || 
	    .@equip_item == 13069 || .@equip_item == 13070 || .@equip_item == 16017) {
		.@type = 1;
		if (.@equip_item == 1490)
			.@lhz_max_num = 4200;
	} else if (.@equip_item == 18109 || .@equip_item == 18110 || .@equip_item == 18111) {
		.@type = 2;
	} else if (.@equip_item == 1584 || .@equip_item == 1659) {
		.@type = 3;
	} else if (.@equip_item == 2160 || .@equip_item == 2161 || .@equip_item == 2162 ||
	           .@equip_item == 2582 || .@equip_item == 2892 || .@equip_item == 15044 || 
	           .@equip_item == 18570) {
		.@type = 4;
		if (.@equip_item == 2582 || .@equip_item == 18570)
			.@lhz_max_num = 4200;
	} else {
		mes "[布丁]";
		mes "在这之前我需要检查一下该装备是否可以附魔.";
		next;
		specialeffect2 EF_SPHERE;
		progressbar "ffff00", 2;
		specialeffect2 EF_SUI_EXPLOSION;
		emotion ET_CRY;
		mes "[布丁]";
		mes "呃, 该装备不能接受'^F2766E勇士的意志^000000' 或者 '^952420血的饥渴^000000'的能力.";
		close;
	}
	emotion ET_DELIGHT;
	mes "[布丁]";
	mes "这装备可以接受 '^F2766E勇士的意志^000000' 或者 '^952420血的饥渴^000000'的能力.";
	next;
	if (countitem(6469) >= 10)
		.@menu_in$[1] = "^F2766勇士的意志^000000的能力";
	else
		.@menu_in$[1] = "^999999'勇士的意志'^000000还不够";

	if (countitem(6470) >= 10)
		.@menu_in$[2] = "'^952420血的饥渴^000000'的能力";
	else
		.@menu_in$[2] = "^999999'血的饥渴'^000000还不够";

	if (countitem(6471) >= 10)
		.@menu_in$[3] = "消费'^88C6F7亡者的寒气^000000'";
	else
		.@menu_in$[3] = "^999999'亡者的寒气'^000000还不够";
	mes "[布丁]";
	mes "好了!你想在哪个装备中注入能力?";
	next;
	switch(select("我不需要!!", .@menu_in$[1], .@menu_in$[2], .@menu_in$[3])) {
	case 1:
		mes "[布丁]";
		mes "哦..我很期待这样做, 但是这一切都取决于你......";
		close;
	case 2:
		if (countitem(6469) < 10) {
			mes "[布丁]";
			mes "'^F2766E勇士的意志^000000'还不够, 你需要10个'^F2766E勇士的意志^000000'";
			close;
		}
		else if (getequipcardid(.@part, 3) > 0) {
			mes "[布丁]";
			mes "这个装备已经获得了'^F2766E勇士的意志^000000'.";
			close;
		} else {
			.@socket_type = 4;
		}
		break;
	case 3:
		if (countitem(6470) < 10) {
			mes "[布丁]";
			mes "'^952420血的饥渴^000000'还不够, 你需要10个'^952420血的饥渴^000000'";
			close;
		}
		else if (getequipcardid(.@part, 2) > 0) {
			mes "[布丁]";
			mes "这个装备已经获得了'^952420血的饥渴^000000'.";
			close;
		} else {
			.@socket_type = 3;
		}
		break;
	case 4:
		// Enchant Removal
		if (countitem(6471) < 10) {
			.@menu_clear$[1] = "^999999'亡者的寒气'^000000还不够";
			.@menu_clear$[2] = "^999999'亡者的寒气'^000000还不够";
			.@4thzptsodyd = 1;
			.@3thzptsodyd = 1;
		} else {
			if (getequipcardid(.@part, 3) > 0) {
				.@menu_clear$[1] = "移除'^F2766E勇士的意志^000000'的能力";
			} else {
				.@menu_clear$[1] = "^999999没有能力可以移除^000000";
				.@4thzptsodyd = 1;
			}
			if (getequipcardid(.@part, 2) > 0) {
				.@menu_clear$[2] = "移除'^952420血的饥渴^000000'的能力";
			} else {
				.@menu_clear$[2] = "^999999没有能力可以移除^000000";
				.@3thzptsodyd = 1;
			}
		}
		mes "[布丁]";
		mes "好吧, 你想要把能力移除?";
		next;
		.@menu = select("不, 我不想.", .@menu_clear$[1], .@menu_clear$[2]);
		switch(.@menu){
		case 1:
			mes "[布丁]";
			mes "真的?好吧..";
			close;
		case 2:
		case 3:
			if (countitem(6471) < 10) {
				mes "[布丁]";
				mes "'^88C6F7亡者的寒气^000000'不够, 你需要10个'^88C6F7亡者的寒气^000000'";
				close;
			}
			if ( (.@menu==3 && .@3thzptsodyd == 1) || (.@menu==2 && .@4thzptsodyd == 1) ) {
				mes "[布丁]";
				mes "该装备没有获得任何能力.";
				close;
			}
			setarray .@equip_card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
			if (.@menu == 2)
				.@equip_card[3] = 0;
			else
				.@equip_card[2] = 0;
			.@equip_item = getequipid(.@part);
			specialeffect EF_BEGINSPELL2, AREA, "巫师#Bio4Reward";
			progressbar "ffff00", 2;
			specialeffect2 EF_FROSTWEAPON;
			delitem 6471, 10; //Goast_Chill
			delequip .@part;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			mes "[布丁]";
			if (.@menu == 3)
				mes "这个'^952420血的饥渴^000000'的能力已经移除.";
			else
				mes "这个'^F2766E勇士的意志^000000'的能力已经移除.";
			close;
		}
	}
	mes "[布丁]";
	mes "在我注入能量之前我给你解释下注意事项.";
	next;
	mes "[布丁]";
	mes "While dwelling the power of '^F2766EWill of Warrior^000000' or '^952420Thirst for Blood^000000', the equipment would be not capable to accept that and will be destryed..";
	next;
	mes "[布丁]";
	mes "^9D2628如果装备被损坏, 你将失去装备上的卡片和精炼度^000000";
	next;
	mes "[布丁]";
	mes "好吧, 你真的想注入能量到装备?";
	next;
	if(select("等等, 我再考虑下.", "当然, 就现在.") == 1) {
		mes "[布丁]";
		mes "好的, 让你考虑下!";
		close;
	}
	mes "[布丁]";
	mes "我希望你做了一个正确的决定...";
	next;
	if (.@socket_type >= 3) {
		switch(.@type){
			case 1:	// Short Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 401)  .@enchant = 4700; //Strength1
				else if (.@r < 441)  .@enchant = 4701; //Strength2
				else if (.@r < 445)  .@enchant = 4702; //Strength3
				else if (.@r < 845)  .@enchant = 4730; //Agility1
				else if (.@r < 885)  .@enchant = 4731; //Agility2
				else if (.@r < 889)  .@enchant = 4732; //Agility3
				else if (.@r < 1289) .@enchant = 4740; //Vitality1
				else if (.@r < 1329) .@enchant = 4741; //Vitality2
				else if (.@r < 1333) .@enchant = 4742; //Vitality3
				else if (.@r < 1523) .@enchant = 4710; //Inteligence1
				else if (.@r < 1542) .@enchant = 4711; //Inteligence2
				else if (.@r < 1544) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1874) .@enchant = 4721; //Dexterity2
				else if (.@r < 1877) .@enchant = 4722; //Dexterity3
				else if (.@r < 2067) .@enchant = 4750; //Luck1
				else if (.@r < 2086) .@enchant = 4751; //Luck2
				else if (.@r < 2088) .@enchant = 4752; //Luck3
				else if (.@r < 2491) .@enchant = 0;
				else if (.@r < 2891) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2931) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2935) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3125) .@enchant = 4815; //Spell1
				else if (.@r < 3144) .@enchant = 4814; //Spell2
				else if (.@r < 3146) .@enchant = 4813; //Spell3
				else if (.@r < 3148) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3348) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 2:	// Long Distance
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 191)  .@enchant = 4700; //Strength1
				else if (.@r < 210)  .@enchant = 4701; //Strength2
				else if (.@r < 212)  .@enchant = 4702; //Strength3
				else if (.@r < 612)  .@enchant = 4730; //Agility1
				else if (.@r < 652)  .@enchant = 4731; //Agility2
				else if (.@r < 656)  .@enchant = 4732; //Agility3
				else if (.@r < 846)  .@enchant = 4740; //Vitality1
				else if (.@r < 865)  .@enchant = 4741; //Vitality2
				else if (.@r < 867)  .@enchant = 4742; //Vitality3
				else if (.@r < 1167) .@enchant = 4710; //Inteligence1
				else if (.@r < 1197) .@enchant = 4711; //Inteligence2
				else if (.@r < 1200) .@enchant = 4712; //Inteligence3
				else if (.@r < 1600) .@enchant = 4720; //Dexterity1
				else if (.@r < 1640) .@enchant = 4721; //Dexterity2
				else if (.@r < 1644) .@enchant = 4722; //Dexterity3
				else if (.@r < 1834) .@enchant = 4750; //Luck1
				else if (.@r < 1853) .@enchant = 4751; //Luck2
				else if (.@r < 1855) .@enchant = 4752; //Luck3
				else if (.@r < 2258) .@enchant = 0;
				else if (.@r < 2448) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2467) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2469) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 2669) .@enchant = 4815; //Spell1
				else if (.@r < 2689) .@enchant = 4814; //Spell2
				else if (.@r < 2691) .@enchant = 4813; //Spell3
				else if (.@r < 2693) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3093) .@enchant = 4818; //Sharp1
				else if (.@r < 3133) .@enchant = 4817; //Sharp2
				else if (.@r < 3137) .@enchant = 4816; //Sharp3
				else if (.@r < 3537) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3577) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 3:
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 534)  .@enchant = 4730; //Agility1
				else if (.@r < 554)  .@enchant = 4731; //Agility2
				else if (.@r < 556)  .@enchant = 4732; //Agility3
				else if (.@r < 956)  .@enchant = 4740; //Vitality1
				else if (.@r < 996)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1400) .@enchant = 4710; //Inteligence1
				else if (.@r < 1440) .@enchant = 4711; //Inteligence2
				else if (.@r < 1444) .@enchant = 4712; //Inteligence3
				else if (.@r < 1844) .@enchant = 4720; //Dexterity1
				else if (.@r < 1884) .@enchant = 4721; //Dexterity2
				else if (.@r < 1888) .@enchant = 4722; //Dexterity3
				else if (.@r < 2078) .@enchant = 4750; //Luck1
				else if (.@r < 2097) .@enchant = 4751; //Luck2
				else if (.@r < 2099) .@enchant = 4752; //Luck3
				else if (.@r < 2502) .@enchant = 0;
				else if (.@r < 2692) .@enchant = 4811; //Fighting_Spirit1
				else if (.@r < 2711) .@enchant = 4810; //Fighting_Spirit2
				else if (.@r < 2713) .@enchant = 4809; //Fighting_Spirit3
				else if (.@r < 3113) .@enchant = 4815; //Spell1
				else if (.@r < 3153) .@enchant = 4814; //Spell2
				else if (.@r < 3157) .@enchant = 4813; //Spell3
				else if (.@r < 3159) .@enchant = 4807; //Atk_Speed1
				else if (.@r < 3349) .@enchant = 4818; //Sharp1
				else if (.@r < 3368) .@enchant = 4817; //Sharp2
				else if (.@r < 3370) .@enchant = 4816; //Sharp3
				else if (.@r < 3560) .@enchant = 4832; //Expert_Archer1
				else if (.@r < 3579) .@enchant = 4833; //Expert_Archer2
				else if (.@r < 3581) .@enchant = 4834; //Expert_Archer3
				else                 .@enchant = 0;
				break;
			case 4:	// Armor
				.@r = rand(1, .@lhz_max_num);
				if      (.@r < 301)  .@enchant = 4700; //Strength1
				else if (.@r < 331)  .@enchant = 4701; //Strength2
				else if (.@r < 334)  .@enchant = 4702; //Strength3
				else if (.@r < 634)  .@enchant = 4730; //Agility1
				else if (.@r < 664)  .@enchant = 4731; //Agility2
				else if (.@r < 667)  .@enchant = 4732; //Agility3
				else if (.@r < 967)  .@enchant = 4740; //Vitality1
				else if (.@r < 997)  .@enchant = 4741; //Vitality2
				else if (.@r < 1000) .@enchant = 4742; //Vitality3
				else if (.@r < 1300) .@enchant = 4710; //Inteligence1
				else if (.@r < 1330) .@enchant = 4711; //Inteligence2
				else if (.@r < 1333) .@enchant = 4712; //Inteligence3
				else if (.@r < 1633) .@enchant = 4720; //Dexterity1
				else if (.@r < 1663) .@enchant = 4721; //Dexterity2
				else if (.@r < 1666) .@enchant = 4722; //Dexterity3
				else if (.@r < 1966) .@enchant = 4750; //Luck1
				else if (.@r < 1996) .@enchant = 4751; //Luck2
				else if (.@r < 1999) .@enchant = 4752; //Luck3
				else if (.@r < 2399) .@enchant = 0;
				else if (.@r < 2549) .@enchant = 4786; //Mdef2
				else if (.@r < 2599) .@enchant = 4787; //Mdef4
				else if (.@r < 2609) .@enchant = 4788; //Mdef6
				else if (.@r < 2614) .@enchant = 4789; //Mdef8
				else if (.@r < 2616) .@enchant = 4790; //Mdef10
				else if (.@r < 2766) .@enchant = 4791; //Def3
				else if (.@r < 2866) .@enchant = 4792; //Def6
				else if (.@r < 2876) .@enchant = 4793; //Def9
				else if (.@r < 2878) .@enchant = 4794; //Def12
				else if (.@r < 3278) .@enchant = 0;
				else if (.@r < 3428) .@enchant = 4859; //Evasion1
				else if (.@r < 3478) .@enchant = 4860; //Evasion3
				else if (.@r < 3488) .@enchant = 4762; //Evasion6
				else if (.@r < 3498) .@enchant = 4800; //SP50
				else if (.@r < 3734) .@enchant = 4795; //HP100
				else if (.@r < 3834) .@enchant = 4796; //HP200
				else if (.@r < 3884) .@enchant = 4797; //HP300
				else                 .@enchant = 0;
			break;
		}
		setarray .@equip_card[0], getequipcardid(.@part,0),getequipcardid(.@part,1),getequipcardid(.@part,2),getequipcardid(.@part,3);
		.@equip_card[(.@socket_type-1)] = .@enchant;
		.@equip_item = getequipid(.@part);
		specialeffect EF_BEGINSPELL3, AREA, "巫师#Bio4Reward";
		progressbar "ffff00", 2;
		if (.@socket_type == 3)
			delitem 6470, 10; //Blood_Thirst
		else
			delitem 6469, 10; //Will_Of_Warrior
		delequip .@part;
		
		if (.@enchant == 0){
			specialeffect2 EF_SUI_EXPLOSION;
			emotion ET_OTL;
			mes "[布丁]";
			mes "唉, 该装备无法接受 "+((.@socket_type==3)?"'^952420血的饥渴^000000'":"'^F2766E勇士的意志^000000'")+", 它将被破坏";
			close;
		} else {
			specialeffect2 EF_FIREHIT;
			getitem2 .@equip_item, 1, 1, .@refine_count, 0, .@equip_card[0], .@equip_card[1], .@equip_card[2], .@equip_card[3];
			emotion ET_DELIGHT;
			mes "[布丁]";
			mes "幸运的是"+((.@socket_type==3)?"'^952420血的饥渴^000000'":"'^F2766E勇士的意志^000000'")+"已经注入到装备上了..";
			close;
		}
	}
	end;
}
