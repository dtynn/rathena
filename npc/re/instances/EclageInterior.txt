//============================================================
//= rAthenaCN 脚本汉化 www.rAthenaCN.com 转自 [ www.99Max.mE ]
//============================================================
//= GIT 代码库 https://github.com/rAthenaCN/rAthenaCN.git
//============================================================
//= 问题反馈 https://github.com/rAthenaCN/rAthenaCN/issues
//============================================================

//===== rAthena Script ======================================= 
//= Eclage Instance Script
//===== By: ================================================== 
//= Dastgir
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Eclage Interior Instance
//===== Additional Comments: ================================= 
//= 1.0 Converted from the official Aegis script. [Dastgir]
//= 1.1 Converted from Hercules script to rAthena script & Small bug fix. [Skorm]
//============================================================ 

ecl_hub01,132,12,3	script	参谋长#tl02	435,{
	.@party_id = getcharid(1);
	.@md_name$ = "Eclage Interior";

	if (.@party_id == 0 || getpartyleader(.@party_id,2) != getcharid(0)){
		mes "-! 警告 !-";
		mes "当前的任务将在纪念地牢里进行.";
		mes "只有组队队长可以进入. 请创建你的队伍.";
		close;
	}
	if (ep14_2_mylord == 29) {
		mes "[神艾美]";
		mes "这个地方设置了能量球照明.";
		mes "如果你喜欢这里, 你应该在这里保持谨慎.";
		next;
		switch (select("进入:放弃")) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[神艾美]";
				mes "不久之前有一些士兵由市长派遣出去了.";
				mes "我应该相信你不是那个他们在寻找的人, 对吗?";
				close;
			}
			ecl_interior_time = gettimetick(2) + 20 * 60;
			mes "[神艾美]";
			mes "由于内部安全问题较为紧张, 你自己应该有所准备.";
			mes "在这里等一会儿.";
			close;
		case 2:
			mes "[神艾美]";
			mes "这可不是一个简单的任务.";
			close;
		}
	}
	mes "[参谋长]";
	mes "嗯? 这个房间?";
	mes "没关系. 不要担心.";
	close;
}

ecl_hub01,130,15,0	script	它是关闭的.	844,{
	if (ep14_2_mylord == 29) {
		mes "这扇门连接到广场的天花板.";
		mes "能量球照明设置的地方看上去更漂亮.";
		next;
		if (select("进入:放弃") == 1) {
			.@party_id = getcharid(1);
			.@md_name$ = "Eclage Interior";
			if (.@party_id == 0 || getpartyleader(.@party_id,2) != getcharid(0)) {
				mes "-!警告 !-";
				mes "当前的任务将在纪念地牢里进行.";
				mes "只有组队队长可以进入. 请创建你的队伍.";
				close;
			}
			if (gettimetick(2) >= ecl_interior_time){
				mes "[神艾美]";
				mes "里面的士兵看上去真的很紧张.";
				mes "引导一个像你这样的局外人是相当困难的.";
				close;
			}
			switch (instance_enter(.@md_name$)) {
			case 3:
				mes "一个未知的错误出现.";
				close;
			case 2:
				mes "关闭?";
				close;
			case 0:
				mapannounce "ecl_hub01",getpartyname(.@party_id) + " 组队队长 " + strcharinfo(0) + " 进入 " + .@md_name$,bc_map,"0x00ff99";
				end;
			default:
			}
		} else
			close;
	}
	mes "这扇门连接到广场的天花板.";
	mes "能量球照明设置的地方看上去更漂亮.";
	mes "目前关闭中.";
	close;
}

1@ecl,58,69,3	script	神艾美#ecl01	435,{
	mes "[神艾美]";
	mes "哦, 这里发生什么事了?";
	mes "他们破坏了市长派出去的军队?";
	next;
	select("什么...?:我打了怪物!");
	mes "[神艾美]";
	mes "没关系.";
	mes "你的工作就此为止.";
	mes "现在你是一个背叛者.";
	mes "别了!";
	specialeffect EF_BEGINSPELL3;
	next;
	mes "[" + strcharinfo(0) + "]";
	mes "没...门!!!";
	specialeffect2 EF_LORD;
	percentheal -99,0;
	soundeffect "wander_man_move.wav",1;
	sc_start SC_BLIND,60000,0;
	close2;
	warp instance_mapname("1@ecl"),146,95;
	end;

OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	hideoffnpc instance_npcname(strnpcinfo(0));
	end;
}

1@ecl,51,64,1	script	可疑的家伙#1	2375,{
	end;
}
1@ecl,43,67,3	duplicate(可疑的家伙#1)	可疑的家伙#2	2375,0,0
1@ecl,55,80,3	duplicate(可疑的家伙#1)	可疑的家伙#3	2375,0,0
1@ecl,60,75,3	duplicate(可疑的家伙#1)	可疑的家伙#4	2375,0,0

1@ecl,42,80,5	script	可疑的家伙#5	2376,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
}
1@ecl,48,86,3	duplicate(可疑的家伙#5)	可疑的家伙#6	2376,0,0
1@ecl,58,82,3	duplicate(可疑的家伙#5)	可疑的家伙#7	2376,0,0
1@ecl,56,88,3	duplicate(可疑的家伙#5)	可疑的家伙#8	2376,0,0

1@ecl,53,83,0	script	#sndmaster	HIDDEN_WARP_NPC,10,10,{
	end;

OnInstanceInit:
	'mob_summoned = 0;
	disablenpc instance_npcname(strnpcinfo(0));
	end;

OnEnable:
	sleep 1000;
    mapannounce instance_mapname("1@ecl"),"警告: 在照明室有异常现象. 在照明室有异常现象.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	sleep 2000;
    mapannounce instance_mapname("1@ecl"),"可疑生物: 呵....哇哇!!",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	hideoffnpc instance_npcname("可疑的家伙#5");
	hideoffnpc instance_npcname("可疑的家伙#6");
	hideoffnpc instance_npcname("可疑的家伙#7");
	hideoffnpc instance_npcname("可疑的家伙#8");
	sleep 3000;
	hideonnpc instance_npcname("#sndmaster");
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#sndmaster")+"::OnMyMobDead") == 0) {
		donpcevent instance_npcname("神艾美#ecl01")+"::OnEnable";
		mapannounce instance_mapname("1@ecl"),"神艾美: 好的, 我及时赶到了.",bc_map,"0xffff00"; //FW_NORMAL 12 0 0
	}
	end;

OnTouch:
	if ('mob_summoned > 1) end;
    mes "在能量球附近消除入侵者, 其他奇怪的生物又出现了.";
    next;
    mes "我们无法与他们沟通, 其中一个因为发现他们的伙伴战败了看上去有点愤怒 .";
    next;
    mes "[" + strcharinfo(0) + "]";
    mes "他们自认为他们是陌若可的化身.";
    mes "能量球的后面是多蒙的国王吗?";
    mes "或者只是巧合?";
    next;
    mes "[多蒙国王的下属]";
    mes "啊...啊!!!";
    mes "哇!";
    next;
    mes "一场战斗在所难免!";
    mes "他们会杀了我的!";
	close2;
	hideonnpc instance_npcname("可疑的家伙#5");
	hideonnpc instance_npcname("可疑的家伙#6");
	hideonnpc instance_npcname("可疑的家伙#7");
	hideonnpc instance_npcname("可疑的家伙#8");
	disablenpc instance_npcname("可疑的家伙#5");
	disablenpc instance_npcname("可疑的家伙#6");
	disablenpc instance_npcname("可疑的家伙#7");
	disablenpc instance_npcname("可疑的家伙#8");
	.@eclmap$ = instance_mapname("1@ecl");
	.@sndmaster$ = instance_npcname("#sndmaster");
	monster .@eclmap$,42,80,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,48,86,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,59,82,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,56,88,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,39,72,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,42,69,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,47,64,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	monster .@eclmap$,50,60,"多蒙国王的下属",2376,1,.@sndmaster$+"::OnMyMobDead";
	'mob_summoned = 2;
    end;
}

1@ecl,57,65,0	script	#event	139,5,5,{
	end;
OnTouch:
	if ('mob_summoned > 0) end;
	.@eclmap$ = instance_mapname("1@ecl");
	.@eventname$ = instance_npcname("#event");
	mes "房间空无一物.";
	mes "灯光从房间中央的一个圆形设备散发出来.";
	next;
	mes "[可疑生物]";
	mes "呵呵......";
	next;
	mes "一群未知生物正在逼近.";
	mes "他们看上去很吃惊,虽然彩虹桥被保留着, 但是很难去联系.";
	next;
	mes "[可疑生物]";
	mes "啊...咔...哇!!";
	next;
	mes "似乎他们马上要发起进攻了.";
	mes "我们忍不住要去他们战斗了!";
	close2;
	hideonnpc instance_npcname("可疑的家伙#1");
	hideonnpc instance_npcname("可疑的家伙#2");
	hideonnpc instance_npcname("可疑的家伙#3");
	hideonnpc instance_npcname("可疑的家伙#4");
	disablenpc instance_npcname("可疑的家伙#1");
	disablenpc instance_npcname("可疑的家伙#2");
	disablenpc instance_npcname("可疑的家伙#3");
	disablenpc instance_npcname("可疑的家伙#4");
	monster .@eclmap$,51,64,"可疑的家伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,43,67,"可疑的家伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,55,80,"可疑的家伙",2375,1,.@eventname$+"::OnMyMobDead";
	monster .@eclmap$,60,75,"可疑的家伙",2375,1,.@eventname$+"::OnMyMobDead";
	'mob_summoned = 1;
	end;

OnMyMobDead:
	if (mobcount(instance_mapname("1@ecl"),instance_npcname("#event")+"::OnMyMobDead") == 0) {
		enablenpc instance_npcname("#sndmaster");
		donpcevent instance_npcname("#sndmaster")+"::OnEnable";
	}
	end;
}

1@ecl,148,97,3	script	阿凡塔#	618,{
	mes "[阿凡塔]";
	mes "嘿, 看看谁在这儿!";
	mes "你是我儿子的朋友, 对吗?";
	mes "多么神奇的缘分把我们一起带到了这里!";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "你发生了什么事吗?";
	mes "那里有一个微妙和神奇的回声.";
	mes "你马上被扔在这里了.";
	next;
	if (select("都是因为你!:神艾美说...") == 1) {
		mes "[阿凡塔]";
		mes "我不太明智你所说的一个单词.";
		mes "你不能整理下你的故事吗? 你不知道 '5个W 和 1个H' 规则吗?";
		next;
	}
	mes "[阿凡塔]";
	mes "哦...这就是发生的事情.";
	mes "正如你所说的, 神艾美在能量球的后面?";
	next;
	mes "[阿凡塔]";
	mes "呵呵...哈哈";
	mes "好的, 好的...他也回家了?";
	mes "我, 所有人的阿凡塔?! 哈哈!";
	cutin "avant02",1;
	next;
	select("你什么意思?");
	mes "[阿凡塔]";
	mes "我在做一个关于能量球的研究.";
	mes "但神艾美一直怂恿我.";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "我从能量球的审美开始去思考, 然后对于能量球内部累计的能量感到疑惑...";
	mes "因为我们都受益于它, 所以我开始探索能量球的原则.";
	next;
	mes "[阿凡塔]";
	mes "所有的拉菲都认为能量球和空气，水一样是很自然的事物, 但他是第一个让我感觉到不同并且想追根溯源.";
	mes "是的...神艾美. 他故意激发了我的好奇心.";
	next;
	mes "[阿凡塔]";
	mes "刺激一个不了解空气和水的人，让他认识并进行调查!";
	mes "好, 好...那不是很有趣!";
	cutin "avant02",1;
	next;
	mes "[阿凡塔]";
	mes "哈...哈哈...";
	mes "...我真的很生气, 我意识到他是如何利用我的，但更糟糕的是，我做了一些对大家可怕的事情.";
	mes "我现在真的非常生气.";
	next;
	select("你有良心吗?");
	mes "[阿凡塔]";
	mes "你认为我是一个怎样的人, 说真的!";
	mes "这是因为我的儿子吗?";
	mes "好的, 那么, 我不能过于苛责你.";
	cutin "avant01",1;
	next;
	mes "[阿凡塔]";
	mes "...哈.";
	mes "这可不好笑.";
	mes "它不再有趣了。我不再对你感兴趣了.";
	next;
	mes "[阿凡塔]";
	mes "......";
	mes "似乎有位访客.";
	next;
	cutin "minuel01",4;
	mes "[敏奴艾]";
	mes "" + strcharinfo(0) + "Sir,";
	mes "因为怀疑最近在照明室的事件与你有关, 所以你被拘留了";
	mes "有目击证人可以证明你的清白.";
	next;
	mes "[敏奴艾]";
	mes "细节我不清楚,";
	mes "但我相信你是无罪的.";
	mes "伤口似乎伤地很严重, 我带你去治疗室.";
	next;
	mes "[敏奴艾]";
	mes "我想着不是什么大不了的事情.";
	mes "...希望吧.";
	mes "我们走吧. 我会引导你.";
	close2;
	cutin "",255;
	warp "1@ecl",146,29;
	end;
}
	
1@ecl,148,29,3	script	洛基#ecl01	512,{
	mes "[洛基]";
	mes "我旁边的这个家伙似乎有很多话要说.";
	mes "为什么你不先和他解释下呢?";
	close;
}	
	
1@ecl,147,30,3	script	守护者努德霍格#ecl01	510,{
	mes "[守护者努德霍格]";
	mes "我想你应该现和海西谈谈.";
	close;
}
	
1@ecl,144,27,5	script	海西#ecl01	623,{
	cutin "hisie01",0;
	mes "[海西]";
	mes "你能告诉我到底发生了什么吗?";
	next;
	select("我与怪兽作战.");
	cutin "hisie03",0;
	mes "[海西]";
	mes "怪兽? 梦罗克的属下?";
	mes "拉菲, 为何你想要杀死我们的士兵!!";
	mes "地球上到底发生了什么?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "是因为这个原因.";
	mes "" + strcharinfo(0) + "它是身体内部 -";
	mes "现在神艾美是多么强大啊.";
	next;
	mes "洛基的手掌上, 有一根小而干燥的刺.";
	next;
	cutin "hisie03",0;
	mes "[海西]";
	mes "神艾美, 队员们的头?";
	mes "为什么他会牵扯到这个事情上去?";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "神艾美是梦克罗的属下之一.";
	mes "通过他身体的一部分, 他的能量能够直接或者间接地控制其他人.";
	next;
	select("现在让我考虑下...");
	mes "[" + strcharinfo(0) + "]";
	mes "来这里之前, 神艾美伸出他的手给我. 当我握住他的手, 我感觉到了火花.";
	mes "我想着只是我的幻觉...也许是当他...";
	next;
	mes "[努德霍格]";
	mes "你也许是对的.";
	mes "他们用了卑鄙的手段.";
	mes "这根刺也许会混淆你的判断.";
	next;
	cutin "hisie03",0;
	mes "[海西]";
	mes "你的意思是他欺骗了我们大家?";
	mes "和我们在一起那么久的拉菲是队员们的头! 他不可能是...";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "",255;
	mes "[洛基]";
	mes "他可能是在良好的环境下.";
	mes "不管怎样, 这家伙是无罪的.";
	mes "我能够证明.";
	next;
	mes "[努德霍格]";
	mes "我以宇宙树监护人的名义发誓.";
	mes "" + strcharinfo(0) + "- 绝不会做这样的事.";
	next;
	cutin "hisie02",0;
	mes "[海西]";
	mes "哈...";
	mes "但是事情已经脱离掌控了.";
	mes "这是我没有料到的.";
	next;
	select(" 能量球发生了什么?");
	mes "[海西]";
	mes "内核被偷了.";
	mes "没人知道能量球的内核不见了,";
	mes "那就是为什么卡尔没有成功的原因.";
	npcskill "AL_HEAL",9,99,80;
	next;
	mes "[海西]";
	mes "还有, 一些士兵因为你受伤了.";
	mes "幸好没有阵亡, 但大部分伤势比较严重.";
	mes "目前情况不妙.";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "有人得担负这个责任.";
	mes "所有可能性," + strcharinfo(0) + "会是你.";
	next;
	cutin "hisie02",0;
	mes "[海西]";
	mes "......有2位男士付清了有关能量球方面的所有费用,";
	mes "但你和卡尔在所难免要承担你们的责任.";
	mes "你会赶上神艾美的.";
	npcskill "AL_HEAL",9,99,80;
	next;
	cutin "hisie01",0;
	mes "[海西]";
	mes "你似乎已经休息够了. 让我们出发吧.";
	mes "卡尔在等你呢.";
	mes "你们两个要做什么?";
	next;
	cutin "",255;
	mes "[洛基]";
	mes "不用为我们担心.";
	mes "我们的目标是魔族国王梦克罗.";
	mes "太好了, 我们在这里发现了一下证据.";
	next;
	cutin "hisie01",0;
	mes "[海西]";
	mes "感谢你的帮助.";
	mes "现在我们要离开了.";
	mes "我们走吧." + strcharinfo(0) + ".";
	ep14_2_mylord = 30;
	changequest 7450,7451;
	close2;
	cutin "",255;
	warp "ecl_in03",57,76;
	instance_destroy;
	end;
}
